// Places each particle's index into its cell's bucket of `sorted`, using an
// atomic bump of the per-cell `cursor`. Final pass of the spatial hash (see
// `particles/grid.rs`). After this, `sorted[starts[c] .. starts[c+1]]` are the
// indices of the particles in cell `c`.

import processing::particles::cell_of;

struct Params {
    grid_min: vec3<f32>,
    cell_size: f32,
    dims_x: u32,
    dims_y: u32,
    dims_z: u32,
    _pad: u32,
}

@group(0) @binding(0) var<storage, read>       position: array<f32>;
@group(0) @binding(1) var<storage, read_write> cursor:   array<atomic<u32>>;
@group(0) @binding(2) var<storage, read_write> sorted:   array<u32>;
@group(0) @binding(3) var<uniform>             params:   Params;

@compute @workgroup_size(64)
fn main(@builtin(global_invocation_id) gid: vec3<u32>) {
    let i = gid.x;
    let n = arrayLength(&position) / 3u;
    if i >= n { return; }
    let p = vec3<f32>(position[i * 3u], position[i * 3u + 1u], position[i * 3u + 2u]);
    let dims = vec3<u32>(params.dims_x, params.dims_y, params.dims_z);
    let slot = atomicAdd(&cursor[cell_of(p, params.grid_min, params.cell_size, dims)], 1u);
    sorted[slot] = i;
}
